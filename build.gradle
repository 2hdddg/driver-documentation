import org.ajoberstar.grgit.*
import org.asciidoctor.groovydsl.AsciidoctorExtensions
import org.asciidoctor.Asciidoctor
import org.asciidoctor.OptionsBuilder
import org.asciidoctor.SafeMode
import de.undercouch.gradle.tasks.download.Download

buildscript {
    ext {
        jrubyVersion = '1.7.23'
        asciidoctorjVersion = '1.5.3.2'
        asciidoctorjPdfVersion = '1.5.0-alpha.10.1'
        asciidoctorGroovyDslVersion = '1.0.0.Alpha1'
        gradleGitVersion = '1.3.2'
    }

    dependencies {
        classpath "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
        classpath "org.asciidoctor:asciidoctorj-pdf:$asciidoctorjPdfVersion"
        classpath "org.asciidoctor:asciidoctorj-groovy-dsl:$asciidoctorGroovyDslVersion"
        classpath "org.ajoberstar:gradle-git:$gradleGitVersion"
        classpath "org.jruby:jruby-complete:$jrubyVersion"
    }

    repositories {
        jcenter()
    }
}

plugins {
    id "de.undercouch.download" version "2.0.0"
}

apply plugin: 'base'

defaultTasks 'asciidoctorHtml', 'asciidoctorPdf'

def projectVersion = '1.0'
def driverLanguages = [
        'java'      : [
                'version': '1.0.0-M01',
                'gitref' : '1.0',
                'url'    : 'http://search.maven.org/#search%7Cga%7C1%7Cg%3A%22org.neo4j.driver%22%20AND%20a%3A%22neo4j-java-driver%22'
        ],
        'javascript': [
                'version': '1.0.0-M01',
                'gitref' : '1.0',
                'url'    : 'https://www.npmjs.com/package/neo4j-driver'
        ],
        'python'    : [
                'version': '1.0.0b1',
                'gitref' : '1.0',
                'url'    : 'https://pypi.python.org/pypi/neo4j-driver'
        ]
]


task('gitClone') {
    description 'Clones the driver sources from GitHub.'
    outputs.dir file("$buildDir/driver-sources")
    doLast {
        driverLanguages.each { lang, info ->
            println "Cloning $lang-driver"
            def targetDir = targetGitdir(lang)
            Grgit.clone(dir: targetDir,
                    uri: "https://github.com/neo4j/neo4j-$lang-driver.git",
                    refToCheckout: info['gitref'])
        }
    }
}

def targetGitdir(lang) {
    file("$buildDir/driver-sources/$lang-driver")
}

def fonts = [
        'Open Sans'  : 'https://www.google.com/fonts/download?kit=3hvsV99qyKCBS55e5pvb3ltkqrIMaAZWyLYEoB48lSQ',
        'Inconsolata': 'https://www.google.com/fonts/download?kit=CNj0Ze1H6w4FVgc32wmZS4fD-WQWLbF4rYwcBGowFYY'
]

task('downloadFonts') {
    description 'Download from Google Fonts.'
    outputs.dir "$buildDir/font-downlods"
    doLast {
        fonts.each { name, url ->
            download {
                src url
                dest "$buildDir/font-downlods/${name}.zip"
                overwrite false
            }
        }
    }
}
downloadFonts.finalizedBy 'unzipFonts'

task('unzipFonts', dependsOn: 'downloadFonts', type: Copy) {
    description 'Unzip zip files with fonts.'
    fonts.each { name, url ->
        from zipTree("$buildDir/font-downlods/${name}.zip")
    }
    into "$buildDir/fonts"
}

task('cleanOutputs', dependsOn: ['cleanAsciidoctorHtml', 'cleanAsciidoctorPdf']) {
    description 'Clean the HTML and PDF outputs.'
}

def commonAsciidoctorAttributes = [
        'project-version'  : projectVersion,
        'icons'            : 'font',
        'imagesdir'        : './images',
        'attribute-missing': 'warn'
]

def htmlAsciidoctorAttributes = [
        'toc'        : 'left',
        'stylesdir'  : 'css/',
        'stylesheet' : 'github.css',
        'sectanchors': true,
        'docinfo1'   : true,
        'linkcss'    : true
]

def pdfAsciidoctorAttributes = [
        'source-highlighter': 'rouge'
]

def adoc = Asciidoctor.Factory.create()

driverLanguages.each { lang, info ->
    commonAsciidoctorAttributes["$lang-driver-version"] = info['version']
    commonAsciidoctorAttributes["$lang-root"] = targetGitdir(lang)
    htmlAsciidoctorAttributes["include-with-$lang"] = true

    AsciidoctorExtensions.extensions {
        block(name: 'include-with-' + lang, contexts: [':open']) {
            parent, reader, attributes ->
                if (parent.getDocument().getAttributes().containsKey('include-with-' + lang)) {
                    createBlock(parent, 'open', reader.readLines(), attributes + ['role': 'include-with-' + lang], [:])
                } else {
                    null
                }
        }
    }
}

def asciidocSourceDir = "${projectDir}/src/main/asciidoc"
def asciidocSourceFile = new File("$asciidocSourceDir/index.adoc")
def htmlOutputDir = "$buildDir/html5"
def pdfOutputDir = "$buildDir/pdf"

task('copyHtmlResources', type: Copy) {
    description 'Copy HTML resources to output.'
    from(asciidocSourceDir) {
        include 'images/**'
    }
    from("${projectDir}/src/main") {
        include 'css/**'
        include 'javascript/**'
    }
    into htmlOutputDir
}

task('asciidoctorHtml', dependsOn: 'gitClone') {
    description 'Build Asciidoctor HTML output.'
    inputs.dir asciidocSourceDir
    outputs.dir htmlOutputDir
    doLast {
        def attrs = commonAsciidoctorAttributes + htmlAsciidoctorAttributes
        def opts = OptionsBuilder.options()
                .backend('html5')
                .safe(SafeMode.UNSAFE)
                .toDir(new File(htmlOutputDir))
                .mkDirs(true)
                .attributes(attrs)
        adoc.convertFile(asciidocSourceFile, opts.get())
    }
}
asciidoctorHtml.finalizedBy 'copyHtmlResources'

task('asciidoctorPdf', dependsOn: 'gitClone') {
    description 'Build Asciidoctor PDF outputs.'
    inputs.dir asciidocSourceDir
    outputs.dir pdfOutputDir
    doLast {
        driverLanguages.each { lang, info ->
            def attrs = commonAsciidoctorAttributes + pdfAsciidoctorAttributes + [('include-with-' + lang): true]
            def opts = OptionsBuilder.options()
                    .backend('pdf')
                    .safe(SafeMode.UNSAFE)
                    .toFile(new File("$pdfOutputDir/neo4j-driver-manual-${lang}.pdf"))
                    .mkDirs(true)
                    .attributes(attrs)
            adoc.convertFile(asciidocSourceFile, opts.get())
        }
    }
}
asciidoctorPdf.onlyIf { project.hasProperty('pdf') }

task buildScriptDependencies(type: org.gradle.api.tasks.diagnostics.DependencyReportTask) {
    description 'Print the build script dependencies.'
    configurations = project.buildscript.configurations
}

