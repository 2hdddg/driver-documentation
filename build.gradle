import org.ajoberstar.grgit.*

defaultTasks 'asciidoctor'

def projectVersion = '1.0'
def asciidoctorjVersion = '1.5.3.2'
def driverLanguages = [
        'java'      : [
                'version': '1.0.0-M01'
        ],
        'javascript': [
                'version': '1.0.0-M01'
        ],
        'python'    : [
                'version': '1.0.0-M01'
        ]
]

buildscript {
    ext {
        jrubyVersion = '1.7.23'
        asciidoctorPluginVersion = '1.5.3'
        asciidoctorjPdfVersion = '1.5.0-alpha.10.1'
        gradleGitVersion = '1.3.2'
    }

    dependencies {
        classpath "org.asciidoctor:asciidoctor-gradle-plugin:$asciidoctorPluginVersion"
        classpath "org.asciidoctor:asciidoctorj-pdf:$asciidoctorjPdfVersion"
        classpath "org.ajoberstar:gradle-git:$gradleGitVersion"
        classpath "org.jruby:jruby-complete:$jrubyVersion"
    }

    repositories {
        jcenter()
    }
}

task('git-clone') {
    description 'Clones the driver sources from GitHub.'
    outputs.dir file("$buildDir/driver-sources")
    doLast {
        driverLanguages.each { lang, info ->
            println "Cloning $lang-driver"
            def targetDir = targetGitdir(lang)
            Grgit.clone(dir: targetDir,
                    uri: "https://github.com/neo4j/neo4j-$lang-driver.git",
                    refToCheckout: 'master')
        }
    }
}

def targetGitdir(lang) {
    file("$buildDir/driver-sources/$lang-driver")
}

def commonAsciidoctorAttributes = [
        'project-version': projectVersion,
        'icons'          : 'font',
        'imagesdir'      : './images',
        'attribute-missing': 'warn'
]

driverLanguages.each { lang, info ->
    commonAsciidoctorAttributes["$lang-driver-version"] = info['version']
    commonAsciidoctorAttributes["$lang-root"] = targetGitdir(lang)
}

def htmlAsciidoctorAttributes = [
        'toc'        : 'left',
        'stylesdir'  : 'css/',
        'stylesheet' : 'github.css',
        'sectanchors': true,
        'docinfo1'   : true,
        'linkcss'    : true
]

def pdfAsciidoctorAttributes = [
        'source-highlighter': 'rouge',
]

apply plugin: 'org.asciidoctor.convert'

asciidoctorj {
    version = asciidoctorjVersion
}

asciidoctor {
    backends 'html5'
    sourceDir "${projectDir}/src/main/asciidoc"
    sources {
        include 'index.adoc'
    }
    resources {
        from(sourceDir) {
            include 'images/**'
        }
        from(sourceDir.getParentFile()) {
            include 'css/**'
            include 'javascript/**'
        }
    }
    attributes commonAsciidoctorAttributes
    attributes htmlAsciidoctorAttributes
}
asciidoctor.dependsOn 'git-clone'

if (project.hasProperty('pdf')) {
    asciidoctor {
        backends 'pdf'
        attributes commonAsciidoctorAttributes
        attributes pdfAsciidoctorAttributes
    }
}

task buildScriptDependencies(type: org.gradle.api.tasks.diagnostics.DependencyReportTask) {
    configurations = project.buildscript.configurations
}

